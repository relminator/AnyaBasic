 # MetaBalls example by Relminator #

 start
    Type Vector2D
    start
         x = 0
         y = 0
    end

    Type MetaBall
    start
        x = 0
        y = 0
        dx = 0
        dy = 0
        r = 1
        g = 1
        b = 1
    end

 	var ScreenWidth =  800
 	var ScreenHeight =  600
 	var frame = 0

 	var gravity = 0.9

    var max = 100
 	MetaBall balls[max]         # Our metaballs #

    var ballImage[1]            # Array to hold the image #

	# Randomizes our ball placements on the screen #
 	function randomizeBallPositions()
 	start
 		# STEP 2 since our type starts at every other element #
 		for( var i = 0 to sizeOf(balls)-1 step 1 )
 		start
 			var x = random(ScreenWidth)
 			var y = random(ScreenHeight*0.99)
 			balls[i].x = x
 			balls[i].y = y
 			balls[i].dx = -2 + random(4)
 			balls[i].dy = 0
 			balls[i].r = random(1)
 			balls[i].g = random(1)
 			balls[i].b = random(1)
 		end
 	end

	# Moves the balls #
 	function moveBalls()
 	start
 		for( var i = 0 to sizeOf(balls)-1 step 1 )
 		start
            balls[i].x = balls[i].x + balls[i].dx
            balls[i].y = balls[i].y + balls[i].dy
            balls[i].dy = balls[i].dy + gravity   # add gravity so they fall #
  		end
 	end

	# Bounces the balls when they hit the floor or they hit the sides #
 	function bounceBalls()
 	start
 		for( var i = 0 to sizeOf(balls)-1 step 1 )
 		start
 			var x = balls[i].x
 			var y = balls[i].y
 			if( x < 0 ) then balls[i].dx = -balls[i].dx              # Change directions #
 			if( x > ScreenWidth ) then balls[i].dx = -balls[i].dx
 			if( y > ScreenHeight ) then balls[i].dy = -20 + - random(20)   # Random bounce #
 		end
 	end

	# Draws the balls as an ellipse #
	function drawBalls()
	start
        setBlendMode(1)  # Glow mode! #
		setColor(1,1,1,1)
		for( var i = 0 to sizeOf(balls)-1 step 1 )
 		start
 			var x = balls[i].x
 			var y = balls[i].y
 			drawTransformedImage( x, y, 0, 10, 10,
                                  balls[i].r,
                                  balls[i].g,
                                  balls[i].b,
                                  1 ),0,ballImage[0]
 		end
	end

	randomizeBallPositions()

	screen(ScreenWidth,ScreenHeight)
	loadimage("metaballsimages/flare.png",  ballImage, sizeof(ballImage)-1)

	while( !(KeyDown(1)) && !WindowClosed() )  # Do the graphics loop until we pressed ESC or closed the window #
	start
		frame = frame + 1
		cls()				# Clear the screen #
		bounceBalls()
		moveBalls()
		drawBalls()
		delay(1) 			# Sleep for 1 milliseconds #
		screenFlip()        # Show the frame buffer #
	end
 	exit(0)  # Clean up graphics#


 end

