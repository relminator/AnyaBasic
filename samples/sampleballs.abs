 # Bouncing Balls sample by Relminator#

 start
    Type Vector2D
    start
         x = 0
         y = 0
    end


 	var ScreenWidth =  800
 	var ScreenHeight =  600
 	var frame = 0

 	var gravity = 0.9

    var max = 100
 	Vector2D ballVectors[max]         # Ball x and y positions #
	Vector2D ballDirections[max]		# Ball x and y directions #

	# Randomizes our ball placements on the screen #
 	function randomizeBallPositions()
 	start
 		# STEP 2 since our type starts at every other element #
 		for( var i = 0 to sizeOf(ballVectors)-1 step 1 )
 		start
 			var x = random(ScreenWidth)
 			var y = random(ScreenHeight*0.75)
 			ballVectors[i].x = x
 			ballVectors[i].y = y
 			ballDirections[i].x = -2 * random(4)
 			ballDirections[i].y = 0
 		end
 	end

	# Moves the balls #
 	function moveBalls()
 	start
 		for( var i = 0 to sizeOf(ballVectors)-1 step 1 )
 		start
            ballVectors[i].x = ballVectors[i].x + ballDirections[i].x
            ballVectors[i].y = ballVectors[i].y + ballDirections[i].y
            ballDirections[i].y = ballDirections[i].y + gravity   # add gravity so they fall #
  		end
 	end

	# Bounces the balls when they hit the floor or they hit the sides #
 	function bounceBalls()
 	start
 		for( var i = 0 to sizeOf(ballVectors)-1 step 1 )
 		start
 			var x = ballVectors[i].x
 			var y = ballVectors[i].y
 			if( x < 0 ) then ballDirections[i].x = -ballDirections[i].x              # Change directions #
 			if( x > ScreenWidth ) then ballDirections[i].x = -ballDirections[i].x
 			if( y > ScreenHeight ) then ballDirections[i].y = -20 + - random(20)   # Random bounce #
 		end
 	end

	# Draws the balls as an ellipse #
	function drawBalls()
	start
		for( var i = 0 to sizeOf(ballVectors)-1 step 1 )
 		start
 			var x = ballVectors[i].x
 			var y = ballVectors[i].y
 			setColor(0.4,0.75,1,1)
		    drawEllipse(x,y,30,30,0)+
 			setColor(1,1,0,1)
		    drawEllipse(x,y,30,30,0)
 		end
	end

	randomizeBallPositions()

	screen(ScreenWidth,ScreenHeight)
	while( !(KeyDown(1)) && !WindowClosed() )  # Do the graphics loop until we pressed ESC or closed the window #
	start
		frame = frame + 1
		cls()				# Clear the screen #
		bounceBalls()
		moveBalls()
		drawBalls()
		delay(14) 			# Sleep for 14 milliseconds #
		screenFlip()        # Show the frame buffer #
	end
 	exit(0)  # Clean up graphics#


 end

