 # Bouncing Balls sample #

 # ********************************************
   This shows how to emulate structs and types
   using a simple 1-dimensional array.
   ******************************************* #

 start

 	var ScreenWidth =  800
 	var ScreenHeight =  600
 	var frame = 0

 	var gravity = 0.9

 	# ************************
 	  ballVectors[0] is x,
 	  ballVectors[1] is y,
 	  ballVectors[2] is x,
 	  ballVectors[3] is z,
 	  and so on...
 	  *********************** #

 	var ballVectors[40]         # Ball x and y positions #
	var ballDirections[40]		# Ball x and y directions #

	# Randomizes our ball placements on the screen #
 	function randomizeBallPositions()
 	start
 		# STEP 2 since our type starts at every other element #
 		for( var i = 0 to 39 step 2 )
 		start
 			var x = random(ScreenWidth)
 			var y = random(ScreenHeight*0.75)
 			ballVectors[i] = x
 			ballDirections[i] = -2 * random(4)
 			var j = i + 1
 			ballVectors[j] = y
 			ballDirections[j] = 0
 		end
 	end

	# Moves the balls #
 	function moveBalls()
 	start
 		for( var i = 0 to 39 step 2 )
 		start
 			var j = i + 1
 			ballVectors[i] = ballVectors[i] + ballDirections[i]
 			ballDirections[i + 1] = ballDirections[j] + gravity     # add gravity so they fall #
 			ballVectors[i + 1] = ballVectors[j] + ballDirections[j]
 		end
 	end

	# Bounces the balls when they hit the floor or they hit the sides #
 	function bounceBalls()
 	start
 		for( var i = 0 to 39 step 2 )
 		start
 			var j = i + 1
 			var x = ballVectors[i]
 			var y = ballVectors[j]
 			if( x < 0 ) then ballDirections[i] = -ballDirections[i]              # Change directions #
 			if( x > ScreenWidth ) then ballDirections[i] = -ballDirections[i]
 			if( y > ScreenHeight ) then ballDirections[j] = -20 + - random(20)   # Random bounce #
 		end
 	end

	# Draws the balls as an ellipse #
	function drawBalls()
	start
		setColor(0.4,0.75,1,1)
		for( var i = 0 to 39 step 2 )
 		start
 			var x = ballVectors[i]
 			var y = ballVectors[i+1]
 			drawEllipse(x,y,30,30,0)+
 		end
	end

	randomizeBallPositions()

	screen(ScreenWidth,ScreenHeight)
	while( !(KeyDown(1)) && !WindowClosed() )  # Do the graphics loop until we pressed ESC or closed the window #
	start
		frame = frame + 1
		cls()				# Clear the screen #
		bounceBalls()
		moveBalls()
		drawBalls()
		delay(14) 			# Sleep for 14 milliseconds #
		screenFlip()        # Show the frame buffer #
	end
 	exit(0)  # Clean up graphics#


 end

